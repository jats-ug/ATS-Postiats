%{
#include "./../ATEXT/atextfun.hats"
%}

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN"
   "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">

<head>
<meta http-equiv="content-type" content="text/html; charset=UTF-8" />
#langeng("
<title>EFFECTIVATS-sorting-linear-lists</title>
")
#langja("
<title>EFFECTIVATS: 線形リストのソート</title>
")
#patscode_style()
#patspage_style()
</head>

<body>

<h1>
#langeng("
Effective ATS:<br>
Sorting Linear Lists
")
#langja("
Effective ATS:<br>
線形リストのソート
")
</h1>

#langeng("
In this article, I present an implementation of merge-sort for sorting
linear lists. While quick-sort is often preferred over merge-sort for
sorting arrays, the latter is likely to be the chosen one for sorting
lists. When employed to sort an array, a serious weakness of merge-sort
lies in its need for additional memory (proportional to the size of the
array) in order to perform merging operations. This weakness does not exist
when merge-sort is called to sort lists. Some strengths of merge-sort
include its being a stable sorting algorithm and its (worst-case)
time-complexity being O(n(log(n)).
")
#langja("
この記事では、線形リストをソートするためのマージソートの実装を紹介します。
配列のソートをするのにマージソートよりもクイックソートがしばしば好まれますが、リストのソートには前者が好まれます。
配列のソートにおいて、マージソートの深刻な弱点はマージ操作を行なうために
(その配列のサイズに比例した)
追加のメモリが必要になってしまうことです。
リストに対するマージソートにこの弱点はありません。
マージソートのいくつかの強みは、それが安定なソートアルゴリズムであり、その
(最悪) 時間的計算量が O(n(log(n)) であることです。
")

<h2>
#langeng("
An abstract interface for linear lists
")
#langja("
線形リストの抽象インターフェイス
")
</h2>

#langeng("
In ATS, [list_vt] is declared as a dataviewtype (that is, linear datatype)
for singly-linked lists.  However, I do not want to make direct use of
[list_vt] here as I intend to give an implementation of merge-sort for
sorting abstract linear lists. So I first introduce an abstract interface
for linear lists to be used in this implementation.
")
#langja("
ATS では、[list_vt] は片方向リスト (singly-linked list) を表わす dataviewtype (つまり線形のデータ型) として宣言されています。
けれども、私はここで [list_vt] を直接使いたくありません。
抽象的な線形リストをソートするマージソートの実装を作りたいのです。
そこで、まずはじめにこの実装で使う線形リストに対する抽象インターフェイスを導入します。
")

<p>

#langeng("
Two abstract types are introduced as follows:
")
#langja("
次のような2つの抽象型を導入します:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
absvtype mynode(l:addr) = ptr(l)
absvtype mylist(l:addr, n:int) = ptr(l)
')</pre>

#langeng("
Given a non-null address L , the type [mynode(L)] is for a list-node
located L. Given a non-null address L and an integer N, the type
[mylist(L, N)] is for a list of length N whose first node is located at L.
Some properties on the parameters of [mylist] are captured by the following
two proof functions:
")
#langja("
NULL ではないアドレス L が与えられた時、型 [mynode(L)] は L に配置されたリストノードを表わします。
NULL ではないアドレス L と整数 N が与えられた時、型 [mylist(L, N)]
は最初のノードが L に配置されるような長さ N のリストを表わします。
[mylist] のパラメータの性質は次の2つの証明関数によって捕捉されます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
praxi
lemma_mylist_param
  {l:addr}{n:int}
  (xs: !mylist(l, n)): [l >= null; n >= 0] void
//
praxi
lemma_mylist_param2
  {l:addr}{n:int}
(
  xs: !mylist(l, n)
) : [(l==null && n==0) || (l > null && n > 0)] void
')</pre>

#langeng("
The two standard list-constructors are assigned the following types:
")
#langja("
標準的な2つのリストのコンストラクタには次の型が割り当てられます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
fun
mylist_nil ()
  : mylist(null, 0) = "mac\#atspre_ptr_null"
//
fun{}
mylist_cons
  {l1,l2:addr}{n:nat}
(
  !mynode(l1) >> mylist(l1,n+1), mylist(l2, n)
) :<!wrt> void // end of [mylist_cons]
//
')</pre>

#langeng("
It is clear that [mylist_cons] needs to perform some assignments so as to
connect a given node (its first argument) to a given list (its second
argument). Sometimes, a given node and a given list are already connected,
requiring no assignments to be performed.  The following proof funtion
[_mylist_cons] is introduced precisely for handling such a case:
")
#langja("
(1番目の引数である) ノードと (2番目の引数である) リストを繋げるために、[mylist_cons]
がなんらかのメモリ割り当てをする必要があるのは明確です。
ときどき、与えられたノードとリストが既に繋がっていて、割り当てが不要なこともあるでしょう。
次の証明関数 [_mylist_cons] はそのような場合を正確に扱うために導入されています:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
prfun
_mylist_cons
  {l1,l2:addr}{n:nat}
(
  !mynode(l1) >> mylist(l1, n+1), mylist(l2, n)
) :<prf> void // end of [_mylist_cons]
//
')</pre>

#langeng("
Also, [mylist_cons2] is introduced as a variant of [mylist_cons]:
")
#langja("
また、[mylist_cons] の変形として [mylist_cons2] も導入されます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
fun{}
mylist_cons2
  {l1,l2:addr}{n:nat}
  (x_hd: mynode(l1), xs_tl: mylist(l2, n)):<!wrt> mylist(l1, n+1)
//
')</pre>

#langeng("
The deconstructors for [mylist], which do the opposite of what the
constructors do, are declared as follows:
")
#langja("
[mylist] に対する、コンストラクタと正反対の動作をするデコンストラクタ (deconstructor) が次のように宣言されます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
prfun
mylist_unnil
  {l:addr} (mylist(l,0)): void
//
fun{}
mylist_uncons
  {l:addr}
  {n:int | n > 0}
(
  xs: !mylist(l, n) >> mynode(l)
) : mylist(n-1) // end-of-fun
//
')</pre>

#langeng("
The meaning of [mylist_unnil] and [mylist_uncons] can be readily
inferred from the types assigned to them. As a variant of [mylist_uncons],
[mylist_uncons2] is given as follows:
")
#langja("
[mylist_unnil] と [mylist_uncons] の意味は、それらに割り当てられた型からすぐに推察できるでしょう。
[mylist_uncons] の変種として [mylist_uncons2] が次のように与えられます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
fun{}
mylist_uncons2
  {l:addr}
  {n:int | n > 0}
  (xs: mylist(l, n)): (mynode(l), mylist(n-1))
//
')</pre>

#langeng("
To see how the constructors and deconstructors for [mylist] can be used,
let us implement a function template [mylist_length] for computing the
length of a given list:
")
#langja("
[mylist] に対するこのコンストラクタとデコンストラクタの使い方を理解するために、与えられたリストの長さを計算する関数テンプレート
[mylist_length] を実装してみましょう:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
fun{}
mylist_length{n:int} (xs: !mylist(n)): int(n)
//
')</pre>

#langeng("
As the implementation for [mylist_cons] and [mylist_uncons] can be made
available only after a concrete representation of [mylist] is chosen,
functions that call either [mylist_cons] or [mylist_uncons] may not be
properly compiled. Therefore, it is important to declare [mylist_length] as
a function template (so that only its instances need to be compiled). An
implementation of [mylist_length] is given as follows:
")
#langja("
[mylist] の具体的な表現が選択された後でのみ [mylist_cons] と [mylist_uncons]
の実装は有効なので、[mylist_cons] もしくは [mylist_uncons] を呼び出す関数も当然コンパイルできません。
従って (そのインスタンスをコンパイルできるように)
関数テンプレートとして [mylist_length] を宣言することが重要になります。
[mylist_length] の実装は次のように与えられます:
")

<pre
class="patsyntax">
#pats2xhtml_dats('\
implement
{}(*tmp*)
mylist_length (xs) = let
//
fun loop
  {i,j:nat} .<i>.
(
  xs: !mylist(i), j: int(j)
) : int(i+j) =
if
isneqz (xs)
then let
  val xs2 = mylist_uncons (xs)
  val res = loop (xs2, j + 1)
  prval () = _mylist_cons (xs, xs2)
in
  res
end // end of [then]
else (j) // end of [else]
//
prval () = lemma_mylist_param (xs)
//
in
  loop (xs, 0)
end // end of [mylist_length]
')</pre>

#langeng("
Note that the symbol [isneqz] is overloaded with the following function:
")
#langja("
記号 [isneqz] は次の関数でオーバーロードされていることに注意してください:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
fun
mylist_isnot_nil
  {l:addr}{n:int}
  (xs: !mylist(l,n)): bool(n > 0) = "mac\#atspre_ptr_isnot_null"
')</pre>

#langeng("
Please find in mylist.dats the entirety of the presented abstract interface
for linear lists.
")
#langja("
ここで紹介した線形リストに対する抽象インターフェイスの全体は mylist.dats から入手できます。
")

<h2>
#langeng("
Merge-sort for sorting abstract linear lists
")
#langja("
抽象線形リストをソートするマージソート
")
</h2>

#langeng("
The following
function template [mylist_mergesort] performs
merge-sort on mylist-values (representing linear lists):
")
#langja("
次の関数テンプレート [mylist_mergesort] は (線形リストを表わす) mylist 値をマージソートします:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
fun{}
mylist_mergesort{n:int} (xs: mylist(n)): mylist(n)
')</pre>

#langeng("
And an implementation of [mylist_mergesort] is given below:
")
#langja("
また [mylist_mergesort] の実装は次のようになります:
")

<pre
class="patsyntax">
#pats2xhtml_dats('\
implement
{}(*tmp*)
mylist_mergesort
  (xs) = let
//
val n = mylist_length (xs)
//
in
  mylist_msort (xs, n)
end // end of [mylist_mergesort]
')</pre>

#langeng("
where [mylist_msort] is declared to be of the following type:
")
#langja("
このとき [mylist_msort] は次の方で宣言されています:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
fun{}
mylist_msort{n:int}
  (xs: mylist(n), n: int(n)): mylist(n)
')</pre>

#langeng("
The code implements [mylist_msort] is given as follows:
")
#langja("
[mylist_msort] の実装は次のように与えられます:
")

<pre
class="patsyntax">
#pats2xhtml_dats('\
implement
{}(*tmp*)
mylist_msort
  (xs, n) = let
in
//
if n >= 2
  then let
//
    val n1 = half (n)
//
    val (xs1, xs2) = mylist_split (xs, n1)
//
    val xs1 = mylist_msort (xs1, n1)
    and xs2 = mylist_msort (xs2, n-n1)
//
  in
    mylist_merge (xs1, xs2)
  end // end of [then]
  else xs // end of [else]
// end of [if]
end // end of [mylist_msort]
')</pre>

#langeng("
where [mylist_split] splits a list into two and
[mylist_merge] merges two sorted lists into one.
")
#langja("
このとき [mylist_split] はリストを2つに分割し、[mylist_merge]
は2つのソート済みリストを1つのリストにマージします。
")

<p>

#langeng("
The function template [mylist_split] is declared
as follows:
")
#langja("
関数テンプレート [mylist_split] は次のように宣言されます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
fun{}
mylist_split
  {n:int}{k:nat | k <= n}
  (xs: mylist(n), k: int(k)): (mylist(k), mylist(n-k))
//
')</pre>

#langeng("
And the following code implements [mylist_split]:
")
#langja("
また [mylist_split] の実装は次のようになります:
")

<pre
class="patsyntax">
#pats2xhtml_dats('\
implement
{}(*tmp*)
mylist_split
  (xs, k) = let
in
//
if
k = 0
then (mylist_nil (), xs)
else let
  val xs_tl =
    mylist_uncons (xs)
  val (xs1, xs2) =
    mylist_split (xs_tl, k-1)
in
  (mylist_cons2 (xs, xs1), xs2)
end // end of [else]
//
end // end of [mylist_split]
')</pre>

<p>

#langeng("
The function template [mylist_merge] is declared
as follows:
")
#langja("
関数テンプレート [mylist_merge] は次のように宣言されます:
")

<pre
class="patsyntax">
#pats2xhtml_sats('\
//
fun{}
mylist_merge
  {n1,n2:int}
  (xs1: mylist(n1), xs2: mylist(n2)): mylist(n1+n2)
//
')</pre>

#langeng("
And the following code implements [mylist_merge]:
")
#langja("
また次のコードは [mylist_merge] を実装しています:
")

<pre
class="patsyntax">
#pats2xhtml_dats('\
implement
{}(*tmp*)
mylist_merge
  (xs1, xs2) = let
//
prval () = lemma_mylist_param (xs1)
prval () = lemma_mylist_param (xs2)
//
in
//
if
isneqz(xs1)
then (
if
isneqz(xs2)
then let
  val xs1_tl = mylist_uncons (xs1)
  val xs2_tl = mylist_uncons (xs2)
  val sgn = compare_mynode_mynode (xs1, xs2)
in
  if sgn <= 0
    then let
      prval () = _mylist_cons (xs2, xs2_tl)
    in
      mylist_cons2 (xs1, mylist_merge (xs1_tl, xs2))
    end // end of [then]
    else let
      prval () = _mylist_cons (xs1, xs1_tl)
    in
      mylist_cons2 (xs2, mylist_merge (xs1, xs2_tl))
    end // end of [else]
end // end of [then]
else let
  prval () = mylist_unnil (xs2) in xs1
end // end of [else]
) (* end of [then] *)
else let
  prval () = mylist_unnil (xs1) in xs2
end // end of [else]
//
end // end of [mylist_merge]
')</pre>

#langeng("
Note that [compare_mynode_mynode] is a function template
for comparing values stored in two given list-nodes.
")
#langja("
[compare_mynode_mynode] は与えられた2つのリストノードに保存されている値を比較する関数テンプレートであることに注意してください。
")

<p>

#langeng("
I want to point out that neither [merge_split] nor [mylist_merge] is
currently implemented as a tail-recursive function. This is primarily for
the sake of giving a more accessible presentation.  However, it should be
an interesting and rewarding exercise to re-implement both [merge_split]
and [mylist_merge] as tail-recursive functions.
")
#langja("
ここでは [merge_split] と [mylist_merge] のどちらも末尾再帰関数として実装されていないことを指摘したいと思います。
ここでの主な目的はより受け入れやすい表現を得ることでした。
けれども、[merge_split] と [mylist_merge] 双方を末尾再帰関数で再実装することは興味深くやりがいのある課題でしょう。
")

<p>

#langeng("
Please find in mylist_mergesort.dats the entirety of the presented
implementation of merge-sort for sorting abstract linear lists.
")
#langja("
ここで紹介した抽象線形リストに対するマージソートの実装の全体は mylist_mergesort.dats から入手できます。
")

<h2>
#langeng("
Merge-sort for sorting singly-linked lists
")
#langja("
片方向リストをソートするマージソート
")
</h2>

#langeng("
In order to merge-sort lists of a concrete representation, we need to
implement [mylist_cons] and [mylist_uncons] based on that representation.
Also, we need to implement [compare_mynode_mynode] base on the
corresponding representaton for list-nodes.
")
#langja("
具体的な表現のリストをマージソートするためには、その表現を用いて [mylist_cons] と [mylist_uncons] を実装する必要があります。
また、リストノードに一致する表現を用いて [compare_mynode_mynode] も実装する必要があります。
")

<p>

#langeng("
Please see mergesort_list_vt.dats for a case where [mylist] is interpreted
as [list_vt].  Also, please see mergesort_sllist.dats where [mylist] is
interpreted as [sllist].
")
#langja("
[mylist] を [list_vt] として解釈した場合については、mergesort_list_vt.dats を参照してください。
また、[mylist] を [sllist] として解釈した場合については、mergesort_sllist.dats を参照してください。
")

<h2>
#langeng("
A side note for quick-sort on singly-linked lists
")
#langja("
片方向リストのクイックソートについて
")
</h2>

#langeng("
For those interested in quick-sort on linear lists, please study the code
in the following files:
")
#langja("
線形リストのクイックソートに興味のある読者には、次のファイルにあるコードから学習することをすすめます:
")

<pre>
mylist_quicklist.dats
quicksort_list_vt.dats
quicksort_sllist.dats
</pre>

#langeng("
which is largly parallel to the presented implementation of merge-sort.
")
#langja("
これらはここで紹介したマージソートの実装に沿っています。
")

<p>

<hr size="2">

#langeng("
This article is written by
<a href=\"http://www.cs.bu.edu/~hwxi/\">Hongwei Xi</a>.
")
#langja("
この記事は
<a href=\"http://www.cs.bu.edu/~hwxi/\">Hongwei Xi</a>
によって書かれ、
<a href=\"http://jats-ug.metasepi.org/\">Japan ATS User Group</a>
によって翻訳されています。
")

#patspage_script()
</body>
</html>

%{
implement main () = fprint_filsub (stdout_ref, "main_atxt.txt")
%}
